
## <summary>policy for lldpd</summary>

########################################
## <summary>
##	Execute lldpd_exec_t in the lldpd domain.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`lldpd_domtrans',`
	gen_require(`
		type lldpd_t, lldpd_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, lldpd_exec_t, lldpd_t)
')

########################################
## <summary>
##      Execute a domain transition to run lldpcli.
## </summary>
## <param name="domain">
##      <summary>
##      Domain allowed to transition.
##      </summary>
## </param>
#
interface(`lldp_cli_domtrans',`
        gen_require(`
                type lldp_cli_t, lldp_cli_exec_t;
        ')

        corecmd_search_bin($1)
        can_exec($1, lldp_cli_exec_t)
        domtrans_pattern($1, lldp_cli_exec_t, lldp_cli_t)
')

########################################
## <summary>
##      Execute lldpcli in the lldp_cli domain,
##      and allow the specified role
##      the lldp_cli domain.
## </summary>
## <param name="domain">
##      <summary>
##      Domain allowed to transition.
##      </summary>
## </param>
## <param name="role">
##      <summary>
##      Role allowed access.
##      </summary>
## </param>
#
interface(`lldp_cli_run',`
        gen_require(`
                type lldp_cli_t;
        ')

        lldp_cli_domtrans($1)
        role $2 types lldp_cli_t;
')

######################################
## <summary>
##	Execute lldpd in the caller domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`lldpd_exec',`
	gen_require(`
		type lldpd_exec_t;
	')

	corecmd_search_bin($1)
	can_exec($1, lldpd_exec_t)
')

########################################
## <summary>
##	Search lldpd conf directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`lldpd_search_conf',`
	gen_require(`
		type lldpd_etc_t;
	')

	allow $1 lldpd_etc_t:dir search_dir_perms;
	files_search_etc($1)
')

########################################
## <summary>
##	Read lldpd conf files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`lldpd_read_conf_files',`
	gen_require(`
		type lldpd_etc_t;
	')

	allow $1 lldpd_etc_t:dir list_dir_perms;
	read_files_pattern($1, lldpd_etc_t, lldpd_etc_t)
	files_search_etc($1)
')

########################################
## <summary>
##	Manage lldpd conf files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`lldpd_manage_conf_files',`
	gen_require(`
		type lldpd_etc_t;
	')

	manage_files_pattern($1, lldpd_etc_t, lldpd_etc_t)
	files_search_etc($1)
')

########################################
## <summary>
##	Create, read, write, and delete
##      lldpd PID files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`lldpd_manage_pid_files',`
	gen_require(`
		type lldpd_var_run_t;
	')

	files_search_pids($1)
	manage_files_pattern($1, lldpd_var_run_t, lldpd_var_run_t)
	manage_dirs_pattern($1, lldpd_var_run_t, lldpd_var_run_t)
')


########################################
## <summary>
##	All of the rules required to administrate
##	an lldpd environment
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`lldp_admin',`
	gen_require(`
		type lldpd_t;
		type lldpd_etc_t;
		type lldpd_var_run_t;
	')

	allow $1 lldpd_t:process { signal_perms };
	ps_process_pattern($1, lldpd_t)

    tunable_policy(`allow_ptrace',`
        allow $1 lldpd_t:process ptrace;
    ')

	files_search_etc($1)
	admin_pattern($1, lldpd_etc_t)

	files_search_pids($1)
	admin_pattern($1, lldpd_var_run_t)

	lldp_cli_run($1, $2)
')
