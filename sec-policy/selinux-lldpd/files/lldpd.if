
## <summary>policy for lldpd</summary>

########################################
## <summary>
##      Execute a domain transition to run lldpcli.
## </summary>
## <param name="domain">
##      <summary>
##      Domain allowed to transition.
##      </summary>
## </param>
#
interface(`lldp_domtrans_cli',`
        gen_require(`
                type lldp_cli_t, lldp_cli_exec_t;
        ')

        corecmd_search_bin($1)
        domtrans_pattern($1, lldp_cli_exec_t, lldp_cli_t)
')

########################################
## <summary>
##      Execute lldpcli in the lldp_cli domain,
##      and allow the specified role
##      the lldp_cli domain.
## </summary>
## <param name="domain">
##      <summary>
##      Domain allowed to transition.
##      </summary>
## </param>
## <param name="role">
##      <summary>
##      Role allowed access.
##      </summary>
## </param>
#
interface(`lldp_cli_run',`
        gen_require(`
                type lldp_cli_t;
        ')

        lldp_domtrans_cli($1)
        role $2 types lldp_cli_t;
')

########################################
## <summary>
##	All of the rules required to administrate
##	an lldpd environment
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`lldp_admin',`
	gen_require(`
		type lldpd_t;
		type lldpd_conf_t;
		type lldpd_runtime_t;
	')

	allow $1 lldpd_t:process { signal_perms };
	ps_process_pattern($1, lldpd_t)

	files_search_etc($1)
	admin_pattern($1, lldpd_conf_t)

	files_search_pids($1)
	admin_pattern($1, lldpd_runtime_t)

	lldp_cli_run($1, $2)
')
